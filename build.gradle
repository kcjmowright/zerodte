plugins {
	id 'java'
	id "com.github.node-gradle.node" version "3.5.0"
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.10.5'
	id 'org.flywaydb.flyway' version '6.4.3'
    id 'com.autonomousapps.dependency-analysis' version '3.4.1'
}

group = 'com.kcjmowright'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	mockitoAgent
}

repositories {
	mavenCentral()
}

var dl4jVersion = '1.0.0-M2.1'

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// implementation 'com.opencsv:opencsv:5.12.0'
	implementation 'ch.obermuhlner:big-math:2.3.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'com.pangility:schwab-api-client:0.1.6'
    implementation("org.deeplearning4j:deeplearning4j-core:$dl4jVersion")
    implementation("org.nd4j:nd4j-api:$dl4jVersion")
    implementation("org.datavec:datavec-api:$dl4jVersion")
    implementation("org.nd4j:nd4j-native:$dl4jVersion:macosx-arm64")
    //implementation("org.bytedeco:openblas:0.3.21-1.5.8:macosx-arm64")
	implementation 'org.bytedeco:openblas-platform:0.3.30-1.5.12'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310' // for proper serialization of java date times.

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly("org.nd4j:nd4j-native-platform:$dl4jVersion")
    runtimeOnly 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

	mockitoAgent 'org.mockito:mockito-core'
}

tasks.named("bootBuildImage") {
    environment = [
            "BP_NATIVE_IMAGE": "true",
            "BP_NATIVE_IMAGE_BUILD_ARGUMENTS": """
				-J-Xmx24g
				-H:NumberOfThreads=4
				--initialize-at-build-time=org.springframework
				--initialize-at-build-time=org.apache.commons.logging
				--initialize-at-build-time=org.slf4j.helpers.BasicMarker
				--initialize-at-build-time=org.aspectj.runtime
				--trace-object-instantiation=ch.qos.logback
				--no-fallback
			""".trim()
    ]
}

test {
	useJUnitPlatform()

	// Configure JVM to load Mockito agent
	//jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}"
	doFirst {
		def agentJar = configurations.mockitoAgent.resolvedConfiguration.resolvedArtifacts.find {
			it.name == 'byte-buddy-agent'
		}
		if (agentJar) {
			jvmArgs "-javaagent:${agentJar.file}"
		}
	}
}

tasks.register('printRuntimeClasspath') {
	def runtimeClasspath = sourceSets.main.runtimeClasspath
	inputs.files(runtimeClasspath)
	doLast {
		println runtimeClasspath.join(File.pathSeparator)
	}
}

node {
	download = true
	workDir = file("${project.projectDir}/.gradle/nodejs")
	npmWorkDir = file("${project.projectDir}/.gradle/npm")
	nodeProjectDir = file("${project.projectDir}/src/ui")
	version = "22.15.0"
}

tasks.register('appNpmInstall', NpmTask) {
	description = " read package.json and install all jars"
	workingDir = file("${project.projectDir}/src/ui")
	args = ["install", "--debug"]
}

tasks.register('buildReactApp', NpmTask) {
	dependsOn 'appNpmInstall'
	description = "Builds application for your frontend "
	workingDir = file("${project.projectDir}/src/ui")
	args = ["run", "build"]
}

tasks.register('copyReactBuild', Copy) {
	dependsOn 'buildReactApp'
	from "${project.projectDir}/src/ui/dist"
	into "${project.projectDir}/src/main/resources/static"
}

processResources.dependsOn copyReactBuild

flyway {
	url = 'jdbc:postgresql://localhost:5432/zerodte'
	user = 'postgres'
	locations = ['classpath:db/migration']
}

